# Nushell Config File

use std *

$env.config = {
  show_banner: false
  edit_mode: vi
}

alias cz = chezmoi
alias less = bat

$env.config.buffer_editor = "code"
$env.EDITOR = "code"
$env.VISUAL = "code"
$env.PAGER = "bat"

# LS_COLORS of the [ayu](https://github.com/ayu-theme) theme generated by 
# [vivid](https://github.com/sharkdp/vivid)
$env.LS_COLORS = ^vivid generate ayu

def --env add-path-if-exists [
  dir: string,
]: nothing -> nothing {
  if ($dir | path exists) {
    path add $dir
  }
}

add-path-if-exists ($nu.home-path | path join ".local" "bin")
add-path-if-exists ($nu.home-path | path join ".local" "share" "bin")
add-path-if-exists ($nu.home-path | path join "bin")
add-path-if-exists ($nu.home-path | path join ".cargo" "bin")
add-path-if-exists ($nu.home-path | path join "scoop" "shims")

## TODO: deno offers bash completions, which we might be able to bring in with carapace
add-path-if-exists ($nu.home-path | path join ".deno" "bin")

let pnpm_home_path = "/root/.local/share/pnpm"
if ($pnpm_home_path | path exists) {
    $env.PNPM_HOME = $pnpm_home_path
    path add $env.PNPM_HOME
}

let bun_install_path = ($nu.home-path | path join ".bun")
if ($bun_install_path | path exists) {
    $env.BUN_INSTALL = $bun_install_path
    path add ($env.BUN_INSTALL | path join "bin")
}

let local_vendor_autoload_path = ($nu.data-dir | path join "vendor" "autoload")
mkdir $local_vendor_autoload_path

# starship, a cross-shell prompt
# https://starship.rs/
$env.PROMPT_INDICATOR_VI_INSERT = $'(ansi green_bold)+(ansi reset) '
$env.PROMPT_INDICATOR_VI_NORMAL = $'(ansi yellow_bold)Δ(ansi reset) '
$env.PROMPT_MULTILINE_INDICATOR = $'(ansi grey)↵(ansi reset) '
starship init nu | save --force ($local_vendor_autoload_path | path join "starship.nu")

# carapace
$env.CARAPACE_BRIDGES = 'zsh,fish,bash,inshellisense'
carapace _carapace nushell | save --force ($local_vendor_autoload_path | path join "carapace.nu")

# fzf
# TODO?: set up with carapace, e.g. `fzf --bash`

# atuin
atuin init nu | save --force ($local_vendor_autoload_path | path join "atuin.nu")

# zoxide
zoxide init nushell | save --force ($local_vendor_autoload_path | path join "zoxide.nu")